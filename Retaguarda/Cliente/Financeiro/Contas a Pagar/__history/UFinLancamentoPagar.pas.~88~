{ *******************************************************************************
Title: T2Ti ERP
Description: Janela Lançamento a Pagar

The MIT License

Copyright: Copyright (C) 2016 T2Ti.COM

Permission is hereby granted, free of charge, to any person
obtaining a copy of this software and associated documentation
files (the "Software"), to deal in the Software without
restriction, including without limitation the rights to use,
copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following
conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.

The author may be contacted at:
t2ti.com@gmail.com</p>

@author Albert Eije (t2ti.com@gmail.com)
@version 2.0
******************************************************************************* }
unit UFinLancamentoPagar;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, UTelaCadastro, DB, DBClient, Menus, StdCtrls, ExtCtrls, Buttons, Grids,
  DBGrids, JvExDBGrids, JvDBGrid, JvDBUltimGrid, ComCtrls, FinLancamentoPagarVO,
  FinLancamentoPagarController, Tipos, Atributos, Constantes, LabeledCtrls, JvToolEdit,
  Mask, JvExMask, JvBaseEdits, Math, StrUtils, ActnList, Generics.Collections,
  RibbonSilverStyleActnCtrls, ActnMan, ToolWin, ActnCtrls, ShellApi, AdmParametroVO,
  System.Actions, Controller;

type
  [TFormDescription(TConstantes.MODULO_CONTAS_PAGAR, 'Lançamento a Pagar')]

  TFFinLancamentoPagar = class(TFTelaCadastro)
    ActionManager: TActionManager;
    ActionGerarParcelas: TAction;
    PanelParcelas: TPanel;
    PanelMestre: TPanel;
    EditIdFornecedor: TLabeledCalcEdit;
    EditFornecedor: TLabeledEdit;
    EditIdDocumentoOrigem: TLabeledCalcEdit;
    EditDocumentoOrigem: TLabeledEdit;
    ComboBoxPagamentoCompartilhado: TLabeledComboBox;
    EditImagemDocumento: TLabeledEdit;
    EditPrimeiroVencimento: TLabeledDateEdit;
    EditQuantidadeParcelas: TLabeledCalcEdit;
    EditValorAPagar: TLabeledCalcEdit;
    EditValorTotal: TLabeledCalcEdit;
    EditDataLancamento: TLabeledDateEdit;
    DSParcelaPagar: TDataSource;
    CDSParcelaPagar: TClientDataSet;
    PageControlItensLancamento: TPageControl;
    tsItens: TTabSheet;
    PanelItensLancamento: TPanel;
    GridParcelas: TJvDBUltimGrid;
    ActionToolBarEdits: TActionToolBar;
    EditNumeroDocumento: TLabeledEdit;
    EditIntervalorEntreParcelas: TLabeledCalcEdit;
    CDSParcelaPagarID: TIntegerField;
    CDSParcelaPagarID_CONTA_CAIXA: TIntegerField;
    CDSParcelaPagarID_FIN_LANCAMENTO_PAGAR: TIntegerField;
    CDSParcelaPagarID_FIN_STATUS_PARCELA: TIntegerField;
    CDSParcelaPagarNUMERO_PARCELA: TIntegerField;
    CDSParcelaPagarDATA_EMISSAO: TDateField;
    CDSParcelaPagarDATA_VENCIMENTO: TDateField;
    CDSParcelaPagarDESCONTO_ATE: TDateField;
    CDSParcelaPagarSOFRE_RETENCAO: TStringField;
    CDSParcelaPagarVALOR: TFMTBCDField;
    CDSParcelaPagarTAXA_JURO: TFMTBCDField;
    CDSParcelaPagarTAXA_MULTA: TFMTBCDField;
    CDSParcelaPagarTAXA_DESCONTO: TFMTBCDField;
    CDSParcelaPagarVALOR_JURO: TFMTBCDField;
    CDSParcelaPagarVALOR_MULTA: TFMTBCDField;
    CDSParcelaPagarVALOR_DESCONTO: TFMTBCDField;
    CDSParcelaPagarPERSISTE: TStringField;
    PanelContaCaixa: TPanel;
    EditIdContaCaixa: TLabeledCalcEdit;
    EditContaCaixa: TLabeledEdit;
    CDSParcelaPagarCONTA_CAIXANOME: TStringField;
    ActionAcionarGed: TAction;
    tsNaturezaFinanceira: TTabSheet;
    PanelNaturezaFinanceira: TPanel;
    JvDBUltimGrid1: TJvDBUltimGrid;
    CDSLancamentoNaturezaFinanceira: TClientDataSet;
    DSLancamentoNaturezaFinanceira: TDataSource;
    CDSLancamentoNaturezaFinanceiraID: TIntegerField;
    CDSLancamentoNaturezaFinanceiraID_FIN_LANCAMENTO_PAGAR: TIntegerField;
    CDSLancamentoNaturezaFinanceiraID_CONTABIL_LANCAMENTO_DET: TIntegerField;
    CDSLancamentoNaturezaFinanceiraID_NATUREZA_FINANCEIRA: TIntegerField;
    CDSLancamentoNaturezaFinanceiraDATA_INCLUSAO: TDateField;
    CDSLancamentoNaturezaFinanceiraVALOR: TFMTBCDField;
    CDSLancamentoNaturezaFinanceiraNATUREZA_FINANCEIRACLASSIFICACAO: TStringField;
    CDSLancamentoNaturezaFinanceiraNATUREZA_FINANCEIRADESCRICAO: TStringField;
    CDSLancamentoNaturezaFinanceiraPERSISTE: TStringField;
    CDSLancamentoNaturezaFinanceiraPERCENTUAL: TFMTBCDField;
    ActionGerarPagamentoFixo: TAction;
    procedure FormCreate(Sender: TObject);
    procedure ActionGerarParcelasExecute(Sender: TObject);
    procedure GridDblClick(Sender: TObject);
    procedure GridParcelasKeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
    procedure CDSParcelaPagarAfterEdit(DataSet: TDataSet);
    procedure CDSParcelaPagarBeforeDelete(DataSet: TDataSet);
    procedure CDSParcelaPagarAfterPost(DataSet: TDataSet);
    procedure GerarParcelas;
    procedure ActionAcionarGedExecute(Sender: TObject);
    procedure JvDBUltimGrid1KeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
    procedure CDSLancamentoNaturezaFinanceiraAfterEdit(DataSet: TDataSet);
    procedure CDSLancamentoNaturezaFinanceiraAfterPost(DataSet: TDataSet);
    procedure EditIdFornecedorKeyUp(Sender: TObject; var Key: Word; Shift: TShiftState);
    procedure EditIdDocumentoOrigemKeyUp(Sender: TObject; var Key: Word; Shift: TShiftState);
    procedure EditIdContaCaixaKeyUp(Sender: TObject; var Key: Word; Shift: TShiftState);
    procedure CDSLancamentoNaturezaFinanceiraBeforePost(DataSet: TDataSet);
    procedure ActionGerarPagamentoFixoExecute(Sender: TObject);
  private
    { Private declarations }
  public
    { Public declarations }
    procedure GridParaEdits; override;
    procedure LimparCampos; override;

    // Controles CRUD
    function DoInserir: Boolean; override;
    function DoEditar: Boolean; override;
    function DoExcluir: Boolean; override;
    function DoSalvar: Boolean; override;

    procedure ConfigurarLayoutTela;
    function VerificarTotalNaturezaFinanceira: Boolean;
  end;

var
  FFinLancamentoPagar: TFFinLancamentoPagar;
  AdmParametroVO: TAdmParametroVO;

implementation

uses ULookup, Biblioteca, UDataModule, PessoaVO, PessoaController,
  FinDocumentoOrigemVO, FinDocumentoOrigemController, FinParcelaPagarVO,
  FinParcelaPagarController, ContaCaixaVO, ContaCaixaController, ViewPessoaFornecedorVO,
  ViewPessoaFornecedorController, NaturezaFinanceiraVO, NaturezaFinanceiraController,
  FinLctoPagarNtFinanceiraVO, AdmParametroController, FinPagamentoFixoVO, FinPagamentoFixoController;
{$R *.dfm}

{$REGION 'Infra'}
procedure TFFinLancamentoPagar.FormCreate(Sender: TObject);
var
  Filtro: String;
begin
  ClasseObjetoGridVO := TFinLancamentoPagarVO;
  ObjetoController := TFinLancamentoPagarController.Create;

  inherited;

  Filtro := 'ID_EMPRESA = ' + IntToStr(Sessao.Empresa.Id);
  AdmParametroVO := TAdmParametroVO(TController.BuscarObjeto('AdmParametroController.TAdmParametroController', 'ConsultaObjeto', [Filtro], 'GET'));

  // Esconde a primeira coluna, que será utilizada na janela de mesclagem

end;

procedure TFFinLancamentoPagar.LimparCampos;
begin
  inherited;
  CDSParcelaPagar.EmptyDataSet;
  CDSLancamentoNaturezaFinanceira.EmptyDataSet;
end;

procedure TFFinLancamentoPagar.ConfigurarLayoutTela;
begin
  PanelEdits.Enabled := True;

  if StatusTela = stNavegandoEdits then
  begin
    PanelMestre.Enabled := False;
    PanelItensLancamento.Enabled := False;
    ActionGerarParcelas.Enabled := False;
  end
  else
  begin
    PanelMestre.Enabled := True;
    PanelItensLancamento.Enabled := True;
    ActionGerarParcelas.Enabled := True;
  end;
end;

function TFFinLancamentoPagar.VerificarTotalNaturezaFinanceira: Boolean;
var
  Total: Extended;
begin
  Total := 0;
  CDSLancamentoNaturezaFinanceira.DisableControls;
  CDSLancamentoNaturezaFinanceira.First;
  while not CDSLancamentoNaturezaFinanceira.Eof do
  begin
    Total := Total + CDSLancamentoNaturezaFinanceiraVALOR.AsExtended;
    CDSLancamentoNaturezaFinanceira.Next;
  end;
  CDSLancamentoNaturezaFinanceira.First;
  CDSLancamentoNaturezaFinanceira.EnableControls;
  Result := (Total = EditValorAPagar.Value);
end;
{$ENDREGION}

{$REGION 'Controles CRUD'}
function TFFinLancamentoPagar.DoInserir: Boolean;
begin
  Result := inherited DoInserir;

  if Result then
  begin
    EditIdFornecedor.SetFocus;
  end;
end;

function TFFinLancamentoPagar.DoEditar: Boolean;
begin
  Result := inherited DoEditar;

  if Result then
  begin
    EditIdFornecedor.SetFocus;
  end;
end;

function TFFinLancamentoPagar.DoExcluir: Boolean;
begin
  if inherited DoExcluir then
  begin
    try
      TController.ExecutarMetodo('FinLancamentoPagarController.TFinLancamentoPagarController', 'Exclui', [IdRegistroSelecionado], 'DELETE', 'Boolean');
      Result := TController.RetornoBoolean;
    except
      Result := False;
    end;
  end
  else
  begin
    Result := False;
  end;

  if Result then
    TController.ExecutarMetodo('FinLancamentoPagarController.TFinLancamentoPagarController', 'Consulta', [Trim(Filtro), Pagina.ToString, False], 'GET', 'Lista');
end;

function TFFinLancamentoPagar.DoSalvar: Boolean;
var
  ParcelaPagar: TFinParcelaPagarVO;
  LancamentoNaturezaFinanceira: TFinLctoPagarNtFinanceiraVO;
begin
  if not CDSLancamentoNaturezaFinanceira.IsEmpty then
  begin
    if not VerificarTotalNaturezaFinanceira then begin
      Application.MessageBox('Os valores informados nas naturezas financeiras não batem com o valor a pagar.', 'Mensagem do Sistema', MB_OK + MB_ICONINFORMATION);
      Exit(False);
    end;
  end;

  Result := inherited DoSalvar;

  if Result then
  begin
    try
      if not Assigned(ObjetoVO) then
        ObjetoVO := TFinLancamentoPagarVO.Create;

      TFinLancamentoPagarVO(ObjetoVO).IdFornecedor := EditIdFornecedor.AsInteger;
      TFinLancamentoPagarVO(ObjetoVO).FornecedorNome := EditFornecedor.Text;
      TFinLancamentoPagarVO(ObjetoVO).IdFinDocumentoOrigem := EditIdDocumentoOrigem.AsInteger;
      TFinLancamentoPagarVO(ObjetoVO).DocumentoOrigemSigla := EditDocumentoOrigem.Text;
      TFinLancamentoPagarVO(ObjetoVO).PagamentoCompartilhado := IfThen(ComboBoxPagamentoCompartilhado.ItemIndex = 0, 'S', 'N');
      TFinLancamentoPagarVO(ObjetoVO).QuantidadeParcela := EditQuantidadeParcelas.AsInteger;
      TFinLancamentoPagarVO(ObjetoVO).ValorTotal := EditValorTotal.Value;
      TFinLancamentoPagarVO(ObjetoVO).ValorAPagar := EditValorAPagar.Value;
      TFinLancamentoPagarVO(ObjetoVO).DataLancamento := EditDataLancamento.Date;
      TFinLancamentoPagarVO(ObjetoVO).NumeroDocumento := EditNumeroDocumento.Text;
      TFinLancamentoPagarVO(ObjetoVO).ImagemDocumento := EditImagemDocumento.Text;
      TFinLancamentoPagarVO(ObjetoVO).PrimeiroVencimento := EditPrimeiroVencimento.Date;
      TFinLancamentoPagarVO(ObjetoVO).IntervaloEntreParcelas := EditIntervalorEntreParcelas.AsInteger;

      // Parcelas
      /// EXERCICIO - TENTE IMPLEMENTAR OS DADOS DA LISTA DE DETALHES DE ACORDO COM O NOVO MODELO PROPOSTO NA INFRA
      TFinLancamentoPagarVO(ObjetoVO).ListaParcelaPagarVO := TObjectList<TFinParcelaPagarVO>.Create;
      CDSParcelaPagar.DisableControls;
      CDSParcelaPagar.First;
      while not CDSParcelaPagar.Eof do
      begin
        if (CDSParcelaPagarPERSISTE.AsString = 'S') or (CDSParcelaPagarID.AsInteger = 0) then
        begin
          ParcelaPagar := TFinParcelaPagarVO.Create;
          ParcelaPagar.Id := CDSParcelaPagarID.AsInteger;
          ParcelaPagar.IdFinLancamentoPagar := TFinLancamentoPagarVO(ObjetoVO).Id;
          ParcelaPagar.IdContaCaixa := CDSParcelaPagarID_CONTA_CAIXA.AsInteger;
          ParcelaPagar.IdFinStatusParcela := AdmParametroVO.FinParcelaAberto;
          ParcelaPagar.NumeroParcela := CDSParcelaPagarNUMERO_PARCELA.AsInteger;
          ParcelaPagar.DataEmissao := CDSParcelaPagarDATA_EMISSAO.AsDateTime;
          ParcelaPagar.DataVencimento := CDSParcelaPagarDATA_VENCIMENTO.AsDateTime;
          ParcelaPagar.DescontoAte := CDSParcelaPagarDESCONTO_ATE.AsDateTime;
          ParcelaPagar.SofreRetencao := CDSParcelaPagarSOFRE_RETENCAO.AsString;
          ParcelaPagar.Valor := CDSParcelaPagarVALOR.AsExtended;

          ParcelaPagar.TaxaJuro := CDSParcelaPagarTAXA_JURO.AsExtended;
          ParcelaPagar.TaxaMulta := CDSParcelaPagarTAXA_MULTA.AsExtended;
          ParcelaPagar.TaxaDesconto := CDSParcelaPagarTAXA_DESCONTO.AsExtended;
          ParcelaPagar.ValorJuro := CDSParcelaPagarVALOR_JURO.AsExtended;
          ParcelaPagar.ValorMulta := CDSParcelaPagarVALOR_MULTA.AsExtended;
          ParcelaPagar.ValorDesconto := CDSParcelaPagarVALOR_DESCONTO.AsExtended;

          TFinLancamentoPagarVO(ObjetoVO).ListaParcelaPagarVO.Add(ParcelaPagar);
        end;

        CDSParcelaPagar.Next;
      end;
      CDSParcelaPagar.EnableControls;

      // Natureza Financeira
      TFinLancamentoPagarVO(ObjetoVO).ListaLancPagarNatFinanceiraVO := TObjectList<TFinLctoPagarNtFinanceiraVO>.Create;
      CDSLancamentoNaturezaFinanceira.DisableControls;
      CDSLancamentoNaturezaFinanceira.First;
      while not CDSLancamentoNaturezaFinanceira.Eof do
      begin
        if (CDSLancamentoNaturezaFinanceiraPERSISTE.AsString = 'S') or (CDSLancamentoNaturezaFinanceiraID.AsInteger = 0) then
        begin
          LancamentoNaturezaFinanceira := TFinLctoPagarNtFinanceiraVO.Create;

          LancamentoNaturezaFinanceira.Id := CDSLancamentoNaturezaFinanceiraID.AsInteger;
          LancamentoNaturezaFinanceira.IdFinLancamentoPagar := CDSLancamentoNaturezaFinanceiraID_FIN_LANCAMENTO_PAGAR.AsInteger;
          LancamentoNaturezaFinanceira.IdNaturezaFinanceira := CDSLancamentoNaturezaFinanceiraID_NATUREZA_FINANCEIRA.AsInteger;
          LancamentoNaturezaFinanceira.DataInclusao := CDSLancamentoNaturezaFinanceiraDATA_INCLUSAO.AsDateTime;
          LancamentoNaturezaFinanceira.Valor := CDSLancamentoNaturezaFinanceiraVALOR.AsExtended;
          LancamentoNaturezaFinanceira.Percentual := CDSLancamentoNaturezaFinanceiraPERCENTUAL.AsExtended;

          TFinLancamentoPagarVO(ObjetoVO).ListaLancPagarNatFinanceiraVO.Add(LancamentoNaturezaFinanceira);
        end;

        CDSLancamentoNaturezaFinanceira.Next;
      end;
      CDSLancamentoNaturezaFinanceira.EnableControls;

      if StatusTela = stInserindo then
      begin
        TController.ExecutarMetodo('FinLancamentoPagarController.TFinLancamentoPagarController', 'Insere', [TFinLancamentoPagarVO(ObjetoVO)], 'PUT', 'Lista');
      end
      else if StatusTela = stEditando then
      begin
        if TFinLancamentoPagarVO(ObjetoVO).ToJSONString <> StringObjetoOld then
        begin
          TController.ExecutarMetodo('FinLancamentoPagarController.TFinLancamentoPagarController', 'Altera', [TFinLancamentoPagarVO(ObjetoVO)], 'POST', 'Boolean');
        end
        else
          Application.MessageBox('Nenhum dado foi alterado.', 'Mensagem do Sistema', MB_OK + MB_ICONINFORMATION);
      end;
    except
      Result := False;
    end;
  end;
end;
{$ENDREGION}

{$REGION 'Campos Transientes'}
procedure TFFinLancamentoPagar.EditIdFornecedorKeyUp(Sender: TObject; var Key: Word; Shift: TShiftState);
var
  Filtro: String;
begin
  if Key = VK_F1 then
  begin
    if EditIdFornecedor.Value <> 0 then
      Filtro := 'ID = ' + EditIdFornecedor.Text
    else
      Filtro := 'ID=0';

    try
      EditIdFornecedor.Clear;
      EditFornecedor.Clear;
      if not PopulaCamposTransientes(Filtro, TViewPessoaFornecedorVO, TViewPessoaFornecedorController) then
        PopulaCamposTransientesLookup(TViewPessoaFornecedorVO, TViewPessoaFornecedorController);
      if CDSTransiente.RecordCount > 0 then
      begin
        EditIdFornecedor.Text := CDSTransiente.FieldByName('ID').AsString;
        EditFornecedor.Text := CDSTransiente.FieldByName('NOME').AsString;
      end
      else
      begin
        Exit;
        EditIdDocumentoOrigem.SetFocus;
      end;
    finally
      CDSTransiente.Close;
    end;
  end;
end;

procedure TFFinLancamentoPagar.EditIdDocumentoOrigemKeyUp(Sender: TObject; var Key: Word; Shift: TShiftState);
var
  Filtro: String;
begin
  if Key = VK_F1 then
  begin
    if EditIdDocumentoOrigem.Value <> 0 then
      Filtro := 'ID = ' + EditIdDocumentoOrigem.Text
    else
      Filtro := 'ID=0';

    try
      EditIdDocumentoOrigem.Clear;
      EditDocumentoOrigem.Clear;
      if not PopulaCamposTransientes(Filtro, TFinDocumentoOrigemVO, TFinDocumentoOrigemController) then
        PopulaCamposTransientesLookup(TFinDocumentoOrigemVO, TFinDocumentoOrigemController);
      if CDSTransiente.RecordCount > 0 then
      begin
        EditIdDocumentoOrigem.Text := CDSTransiente.FieldByName('ID').AsString;
        EditDocumentoOrigem.Text := CDSTransiente.FieldByName('SIGLA_DOCUMENTO').AsString;
      end
      else
      begin
        Exit;
        EditNumeroDocumento.SetFocus;
      end;
    finally
      CDSTransiente.Close;
    end;
  end;
end;

procedure TFFinLancamentoPagar.EditIdContaCaixaKeyUp(Sender: TObject; var Key: Word; Shift: TShiftState);
var
  Filtro: String;
begin
  if Key = VK_F1 then
  begin
    if EditIdContaCaixa.Value <> 0 then
      Filtro := 'ID = ' + EditIdContaCaixa.Text
    else
      Filtro := 'ID=0';

    try
      EditIdContaCaixa.Clear;
      EditContaCaixa.Clear;
      if not PopulaCamposTransientes(Filtro, TContaCaixaVO, TContaCaixaController) then
        PopulaCamposTransientesLookup(TContaCaixaVO, TContaCaixaController);
      if CDSTransiente.RecordCount > 0 then
      begin
        EditIdContaCaixa.Text := CDSTransiente.FieldByName('ID').AsString;
        EditContaCaixa.Text := CDSTransiente.FieldByName('NOME').AsString;
      end
      else
      begin
        Exit;
        GridParcelas.SetFocus;
      end;
    finally
      CDSTransiente.Close;
    end;
  end;
end;
{$ENDREGION}

{$REGION 'Controle de Grid'}
procedure TFFinLancamentoPagar.GridDblClick(Sender: TObject);
begin
  inherited;
  ConfigurarLayoutTela;
end;

procedure TFFinLancamentoPagar.GridParaEdits;
var
  ParcelaPagarEnumerator: TEnumerator<TFinParcelaPagarVO>;
  LancamentoNaturezaFinanceiraEnumerator: TEnumerator<TFinLctoPagarNtFinanceiraVO>;
begin
  inherited;

  if not CDSGrid.IsEmpty then
  begin
    ObjetoVO := TFinLancamentoPagarVO(TController.BuscarObjeto('FinLancamentoPagarController.TFinLancamentoPagarController', 'ConsultaObjeto', ['ID=' + IdRegistroSelecionado.ToString], 'GET'));
  end;

  if Assigned(ObjetoVO) then
  begin
    EditIdFornecedor.AsInteger := TFinLancamentoPagarVO(ObjetoVO).IdFornecedor;
    EditFornecedor.Text := TFinLancamentoPagarVO(ObjetoVO).FornecedorNome;
    EditIdDocumentoOrigem.AsInteger := TFinLancamentoPagarVO(ObjetoVO).IdFinDocumentoOrigem;
    EditDocumentoOrigem.Text := TFinLancamentoPagarVO(ObjetoVO).DocumentoOrigemSigla;
    ComboBoxPagamentoCompartilhado.ItemIndex := IfThen(TFinLancamentoPagarVO(ObjetoVO).PagamentoCompartilhado = 'S', 0, 1);
    EditQuantidadeParcelas.AsInteger := TFinLancamentoPagarVO(ObjetoVO).QuantidadeParcela;
    EditValorTotal.Value := TFinLancamentoPagarVO(ObjetoVO).ValorTotal;
    EditValorAPagar.Value := TFinLancamentoPagarVO(ObjetoVO).ValorAPagar;
    EditDataLancamento.Date := TFinLancamentoPagarVO(ObjetoVO).DataLancamento;
    EditNumeroDocumento.Text := TFinLancamentoPagarVO(ObjetoVO).NumeroDocumento;
    EditImagemDocumento.Text := TFinLancamentoPagarVO(ObjetoVO).ImagemDocumento;
    EditPrimeiroVencimento.Date := TFinLancamentoPagarVO(ObjetoVO).PrimeiroVencimento;
    EditIntervalorEntreParcelas.AsInteger := TFinLancamentoPagarVO(ObjetoVO).IntervaloEntreParcelas;

    // Preenche as grids internas com os dados das Listas que vieram no objeto
    // Parcelas
    TController.TratarRetorno<TFinParcelaPagarVO>(TFinLancamentoPagarVO(ObjetoVO).ListaParcelaPagarVO, True, True, CDSParcelaPagar);
    // Natureza Financeira
    TController.TratarRetorno<TFinLctoPagarNtFinanceiraVO>(TFinLancamentoPagarVO(ObjetoVO).ListaLancPagarNatFinanceiraVO, True, True, CDSLancamentoNaturezaFinanceira);

    // Limpa as listas para comparar posteriormente se houve inclusões/alterações e subir apenas o necessário para o servidor
    TFinLancamentoPagarVO(ObjetoVO).ListaParcelaPagarVO.Clear;
    TFinLancamentoPagarVO(ObjetoVO).ListaLancPagarNatFinanceiraVO.Clear;

    // Serializa o objeto para consultar posteriormente se houve alterações
    FormatSettings.DecimalSeparator := '.';
    StringObjetoOld := ObjetoVO.ToJSONString;
    FormatSettings.DecimalSeparator := ',';
  end;
  ConfigurarLayoutTela;
end;

procedure TFFinLancamentoPagar.JvDBUltimGrid1KeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
var
  Filtro: String;
begin
  if Key = VK_F1 then
  begin
    try
      PopulaCamposTransientesLookup(TNaturezaFinanceiraVO, TNaturezaFinanceiraController);
      if CDSTransiente.RecordCount > 0 then
      begin
        CDSLancamentoNaturezaFinanceira.Append;
        CDSLancamentoNaturezaFinanceiraID_NATUREZA_FINANCEIRA.AsInteger := CDSTransiente.FieldByName('ID').AsInteger;
        CDSLancamentoNaturezaFinanceiraDATA_INCLUSAO.AsDateTime := Now;
        CDSLancamentoNaturezaFinanceiraNATUREZA_FINANCEIRACLASSIFICACAO.AsString := CDSTransiente.FieldByName('CLASSIFICACAO').AsString;
        CDSLancamentoNaturezaFinanceiraNATUREZA_FINANCEIRADESCRICAO.AsString := CDSTransiente.FieldByName('DESCRICAO').AsString;
      end;
    finally
      CDSTransiente.Close;
    end;
  end;
end;

procedure TFFinLancamentoPagar.GridParcelasKeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
begin
  If Key = VK_RETURN then
    GridParcelas.SelectedIndex := GridParcelas.SelectedIndex + 1;
end;

procedure TFFinLancamentoPagar.CDSLancamentoNaturezaFinanceiraAfterEdit(DataSet: TDataSet);
begin
  CDSLancamentoNaturezaFinanceiraPERSISTE.AsString := 'S';
end;

procedure TFFinLancamentoPagar.CDSLancamentoNaturezaFinanceiraAfterPost(DataSet: TDataSet);
begin
  if CDSLancamentoNaturezaFinanceiraID_NATUREZA_FINANCEIRA.AsInteger = 0 then
    CDSLancamentoNaturezaFinanceira.Delete;
end;

procedure TFFinLancamentoPagar.CDSLancamentoNaturezaFinanceiraBeforePost(DataSet: TDataSet);
begin
  CDSLancamentoNaturezaFinanceiraPERCENTUAL.AsExtended := (CDSLancamentoNaturezaFinanceiraVALOR.AsExtended / EditValorAPagar.Value) * 100;
end;

procedure TFFinLancamentoPagar.CDSParcelaPagarAfterEdit(DataSet: TDataSet);
begin
  CDSParcelaPagarPERSISTE.AsString := 'S';
end;

procedure TFFinLancamentoPagar.CDSParcelaPagarAfterPost(DataSet: TDataSet);
begin
  if CDSParcelaPagarNUMERO_PARCELA.AsInteger = 0 then
    CDSParcelaPagar.Delete;
end;

procedure TFFinLancamentoPagar.CDSParcelaPagarBeforeDelete(DataSet: TDataSet);
begin
  if CDSParcelaPagarID.AsInteger > 0 then
    TFinParcelaPagarController.Exclui(CDSParcelaPagarID.AsInteger);
end;
{$ENDREGION}

{$REGION 'Actions'}
procedure TFFinLancamentoPagar.ActionAcionarGedExecute(Sender: TObject);
var
  Parametros: String;
begin
  if EditNumeroDocumento.Text <> '' then
  begin
    {
      Parametros
      1 - Login
      2 - Senha
      3 - Aplicação que chamou
      4 - Nome do arquivo (Aplicacao que chamou + Tela que chamou + Numero Apólice
      }

    try
      EditImagemDocumento.Text := 'LANCAMENTO_PAGAR_' + MD5String(EditNumeroDocumento.Text);

      Parametros := Sessao.Usuario.Login + ' ' +
                    Sessao.Usuario.Senha + ' ' +
                    'FINANCEIRO' + ' ' +
                    EditImagemDocumento.Text;
      ShellExecute(
            Handle,
            'open',
            'T2TiERPGed.exe',
            PChar(Parametros),
            '',
            SW_SHOWNORMAL
            );
    except
      Application.MessageBox('Erro ao tentar executar o módulo.', 'Erro do Sistema', MB_OK + MB_ICONERROR);
    end;
  end
  else
  begin
    Application.MessageBox('É preciso informar o número do documento.', 'Mensagem do Sistema', MB_OK + MB_ICONINFORMATION);
    EditNumeroDocumento.SetFocus;
  end;
end;

procedure TFFinLancamentoPagar.ActionGerarParcelasExecute(Sender: TObject);
begin
  if EditIdContaCaixa.AsInteger <=0 then
  begin
    Application.MessageBox('É necessário informar a conta/caixa para previsão das parcelas.', 'Mensagem do Sistema', MB_OK + MB_ICONINFORMATION);
    EditIdContaCaixa.SetFocus;
    Exit;
  end;

  if not CDSParcelaPagar.IsEmpty then
  begin
    if Application.MessageBox('Já existem parcelas geradas e serão excluídas. Deseja continuar?', 'Pergunta do Sistema', MB_YesNo + MB_IconQuestion) = IdYes then
    begin
      CDSParcelaPagar.DisableControls;
      CDSParcelaPagar.First;
      while not CDSParcelaPagar.Eof do
      begin
        if CDSParcelaPagarID.AsInteger > 0 then
          TFinParcelaPagarController.Exclui(CDSParcelaPagarID.AsInteger);

        CDSParcelaPagar.Next;
      end;
      CDSParcelaPagar.First;
      CDSParcelaPagar.EnableControls;

      CDSParcelaPagar.EmptyDataSet;
      GerarParcelas;
    end;
  end
  else
    GerarParcelas;
end;

procedure TFFinLancamentoPagar.GerarParcelas;
var
  i: integer;
  Vencimento: TDate;
  SomaParcelas, Residuo: Extended;
  FornecedorVO: TViewPessoaFornecedorVO;
  Filtro: String;
begin
  if EditQuantidadeParcelas.AsInteger <= 0 then
    EditQuantidadeParcelas.AsInteger := 1;

  if EditPrimeiroVencimento.Text = '  /  /    ' then
    EditPrimeiroVencimento.Date := Date;

  Vencimento := EditPrimeiroVencimento.Date;
  SomaParcelas := 0;
  Residuo := 0;

  Filtro := 'ID=' + QuotedStr(EditIdFornecedor.Text);
  FornecedorVO := TViewPessoaFornecedorVO(TController.BuscarObjeto('ViewPessoaFornecedorController.TViewPessoaFornecedorController', 'ConsultaObjeto', [Filtro], 'GET'));

  for i := 0 to EditQuantidadeParcelas.AsInteger - 1 do
  begin
    CDSParcelaPagar.Append;
    CDSParcelaPagar.FieldByName('NUMERO_PARCELA').AsInteger := i+1;
    CDSParcelaPagar.FieldByName('ID_CONTA_CAIXA').AsInteger := EditIdContaCaixa.AsInteger;
    CDSParcelaPagar.FieldByName('DATA_EMISSAO').AsString := DateToStr(Date);
    CDSParcelaPagar.FieldByName('DATA_VENCIMENTO').AsString := DateToStr(Vencimento + (EditIntervalorEntreParcelas.AsInteger * i));
    CDSParcelaPagar.FieldByName('DESCONTO_ATE').AsString := DateToStr(Date);
    CDSParcelaPagar.FieldByName('SOFRE_RETENCAO').AsString := FornecedorVO.SofreRetencao;
    CDSParcelaPagar.FieldByName('VALOR').AsFloat := ArredondaTruncaValor('A', EditValorAPagar.Value / EditQuantidadeParcelas.AsInteger, Constantes.TConstantes.DECIMAIS_VALOR);
    CDSParcelaPagar.FieldByName('CONTA_CAIXA.NOME').AsString := EditContaCaixa.Text;
    CDSParcelaPagar.Post;

    SomaParcelas := SomaParcelas + CDSParcelaPagar.FieldByName('VALOR').AsFloat;
  end;

  // calcula o resíduo e lança na última parcela
  Residuo := EditValorAPagar.Value - SomaParcelas;
  CDSParcelaPagar.Edit;
  CDSParcelaPagar.FieldByName('VALOR').AsFloat := CDSParcelaPagar.FieldByName('VALOR').AsFloat + Residuo;
  CDSParcelaPagar.Post;
end;

procedure TFFinLancamentoPagar.ActionGerarPagamentoFixoExecute(Sender: TObject);
var
  PagamentoFixoVO: TFinPagamentoFixoVO;
begin
  /// EXERCICIO - VERIFIQUE SE ESSE PAGAMENTO JA FOI MARCADO COMO FIXO E AJA DE ACORDO
  ///  01 - APAGUE O REGISTRO ANTERIOR E GRAVE O NOVO
  ///  02 - ALTERE O REGISTRO ANTERIOR
  ///  03 - INFORME QUE JÁ EXISE UM PAGAMENTO FIXO ARMAZENADO E QUE NÃO É POSSÍVEL REALIZAR ALTERAÇÕES
  ///  04 - SOLICITE UMA AÇÃO DO USUÁRIO

  if Application.MessageBox('Deseja gravar esse pagamento como recorrente? (Lançamento Mensal)', 'Pergunta do Sistema', MB_YesNo + MB_IconQuestion) = IdYes then
  begin
    PagamentoFixoVO := TFinPagamentoFixoVO.Create;

    PagamentoFixoVO.IdFornecedor := EditIdFornecedor.AsInteger;
    PagamentoFixoVO.IdFinDocumentoOrigem := EditIdDocumentoOrigem.AsInteger;
    PagamentoFixoVO.PagamentoCompartilhado := IfThen(ComboBoxPagamentoCompartilhado.ItemIndex = 0, 'S', 'N');
    PagamentoFixoVO.QuantidadeParcela := EditQuantidadeParcelas.AsInteger;
    PagamentoFixoVO.ValorTotal := EditValorTotal.Value;
    PagamentoFixoVO.ValorAPagar := EditValorAPagar.Value;
    PagamentoFixoVO.DataLancamento := EditDataLancamento.Date;
    PagamentoFixoVO.NumeroDocumento := EditNumeroDocumento.Text;
    PagamentoFixoVO.ImagemDocumento := EditImagemDocumento.Text;
    PagamentoFixoVO.PrimeiroVencimento := EditPrimeiroVencimento.Date;
    PagamentoFixoVO.IntervaloEntreParcelas := EditIntervalorEntreParcelas.AsInteger;

    TController.ExecutarMetodo('FinPagamentoFixoController.TFinPagamentoFixoController', 'Insere', [PagamentoFixoVO], 'PUT', 'Lista');

    Application.MessageBox('Lançamento realizado com sucesso.', 'Mensagem do Sistema', MB_OK + MB_ICONINFORMATION);
  end;
end;

{$ENDREGION}

end.
